apply plugin: 'com.android.application'

//true 为正式版本 false 为调试版本
ext.debug = false
ext.majorNumber = 1
ext.minorNumber = 0
//git 获取当前分支commit次数
ext.revisionNumberCMD = 'git rev-list HEAD --count'
ext.revisionNumber = revisionNumberCMD.execute().getText().trim().toInteger()
//git最后一次commit识别码后六位
ext.revisionDescriptionCMD = 'git describe --always'
ext.tempRevisionDescription = revisionDescriptionCMD.execute().getText().trim()
ext.revisionDescription = (tempRevisionDescription == null || (tempRevisionDescription).size() == 0) ?
        new Date().format("yyMMdd") : (tempRevisionDescription).substring((tempRevisionDescription).size() - 6)
android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.andeddo.lanchat"
        minSdkVersion 22
        targetSdkVersion 26
        versionCode getVersionCode(debug)
        versionName getVersionName(debug)
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                outputFileName = defaultConfig.applicationId.subSequence(
                        defaultConfig.applicationId.lastIndexOf(".") + 1,
                        defaultConfig.applicationId.length()) + "-v" +
                        defaultConfig.versionCode +
                        "." + releaseTime() + "-" + output.baseName + ".apk"
            }
        }
    }
}

static def releaseTime() {
    return new Date().format("HHmmss", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':library')
}

//获取编译版本号
def getVersionCode(boolean isRelease) {
    if (isRelease) {
        // 正式环境
        majorNumber * 1000000 + minorNumber * 10000 + revisionNumber
    } else {
        // debug环境
        Integer.parseInt(new Date().format("yyMMddHHmm"))
    }
}

//获取编译版本名称
def getVersionName(boolean isRelease) {
    if (isRelease) {
        // 正式环境
        majorNumber + "." + minorNumber + "." + revisionNumber + "." + revisionDescription
    } else {
        // debug环境
        String today = new Date().format("yyMMdd")
        String time = new Date().format("HHmmss")
        majorNumber + "." + minorNumber + "." + revisionNumber + ".$today.$time." + revisionDescription
    }
}